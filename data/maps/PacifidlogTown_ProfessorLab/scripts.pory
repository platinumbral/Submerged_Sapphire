mapscripts PacifidlogTown_ProfessorLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION : PacifidlogTown_ProfessorLab_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_2, 0: PacifidlogTown_ProfessorLab_EventScript_EveryFrame
        ]
}    

script PacifidlogTown_ProfessorLab_OnTransition {
    setvar(VAR_TEMP_2, 0)
}

script PacifidlogTown_ProfessorLab_EventScript_EveryFrame {
    if(var(VAR_PACIFIDLOG_TOWN_STATE) == 1) {
        lockall
        setvar(VAR_TEMP_2, 1)
        msgbox(format("Alkalinity and turbidity readings are normal..."))
        waitmessage
        applymovement(3, ProfessorIntro1)
        waitmovement(0)
        msgbox(format("Looks like there's some minor bleaching in the colony..."))
        waitmessage
        applymovement(3, ProfessorIntro2)
        waitmovement(0)
        msgbox(format("Oh, {PLAYER}! There you are!"))
        waitmessage
        msgbox(format("Come here, I want to give you something!"))
        waitmessage
        applymovement(3, ProfessorIntro3)
        applymovement(OBJ_EVENT_ID_PLAYER, PlayerIntro)
        waitmovement(0)
        msgbox(format("There are three Poké Balls here, each containing a different Pokémon. I want you to choose one!"))
        setvar(VAR_PACIFIDLOG_TOWN_STATE, 2)
        waitmessage
        releaseall
    }
}

script PacifidlogTown_ProfessorLab_EventScript_LeftBall {
    lockall
    if(!(flag(FLAG_LEFT_STARTER) || flag(FLAG_MID_STARTER) || flag(FLAG_RIGHT_STARTER))) {
        showmonpic(SPECIES_PIPLUP, 10, 3)
        msgbox("Do you want to choose Piplup?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES) {
            playfanfare(MUS_OBTAIN_BADGE)
            msgbox("You received Piplup!")
            givemon(SPECIES_PIPLUP, 5)
            removeobject(4)
            setflag(FLAG_LEFT_STARTER)
            setflag(FLAG_SYS_POKEMON_GET)
            setvar(VAR_STARTER_MON, 0)
            setvar(VAR_PACIFIDLOG_TOWN_STATE, 3)
            waitfanfare
            call(NicknamePartyMon)
        }
        else{
            hidemonpic
        }
    }
    else{
        msgbox("You already have a starter Pokémon.")
    }
    waitmessage
    releaseall
    end
}

script PacifidlogTown_ProfessorLab_EventScript_MiddleBall {
    lockall
    if(!(flag(FLAG_LEFT_STARTER) || flag(FLAG_MID_STARTER) || flag(FLAG_RIGHT_STARTER))) {
        showmonpic(SPECIES_OSHAWOTT, 10, 3)
        msgbox("Do you want to choose Oshawott?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES) {
            playfanfare(MUS_OBTAIN_BADGE)
            msgbox("You received Oshawott!")
            givemon(SPECIES_OSHAWOTT, 5)
            removeobject(5)
            setflag(FLAG_MID_STARTER)
            setflag(FLAG_SYS_POKEMON_GET)
            setvar(VAR_STARTER_MON, 1)
            setvar(VAR_PACIFIDLOG_TOWN_STATE, 3)
            waitfanfare
            call(NicknamePartyMon)
        }
        else{
            hidemonpic
        }
    }
    else{
        msgbox("You already have a starter Pokémon.")
    }
    waitmessage
    releaseall
    end
}

script PacifidlogTown_ProfessorLab_EventScript_RightBall {
    lockall
    if(!(flag(FLAG_LEFT_STARTER) || flag(FLAG_MID_STARTER) || flag(FLAG_RIGHT_STARTER))) {
        showmonpic(SPECIES_POPPLIO, 10, 3)
        msgbox("Do you want to choose Popplio?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES) {
            playfanfare(MUS_OBTAIN_BADGE)
            msgbox("You received Popplio!")
            givemon(SPECIES_POPPLIO, 5)
            removeobject(6)
            setflag(FLAG_RIGHT_STARTER)
            setflag(FLAG_SYS_POKEMON_GET)
            setvar(VAR_STARTER_MON, 2)
            setvar(VAR_PACIFIDLOG_TOWN_STATE, 3)
            waitfanfare
            call(NicknamePartyMon)
        }
        else{
            hidemonpic
        }
    }
    else{
        msgbox("You already have a starter Pokémon.")
    }
    waitmessage
    releaseall
    end
}

script PacifidlogTown_ProfessorLab_EventScript_Professor {
    lock
    faceplayer
    msgbox(format("Hello {PLAYER}! The Corsola seem to be doing well today!"))
    release
}

script PacifidlogTown_ProfessorLab_EventScript_Aide1 {
    lock
    faceplayer
    msgbox(format("I know the work we do here is important to keep the Corsola healthy, but I wish the seawater didn't spray all over my papers..."))
    release
}

script PacifidlogTown_ProfessorLab_EventScript_Aide2 {
    lock
    faceplayer
    msgbox(format("A long time ago, Mareanie didn't live in Hoenn. But now, they've been spread head to Pacifidlog, and pose a real threat to our Corsola colony. "))
    release
}

script NicknamePartyMon {
    msgbox("Would you like to give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script Pacifidlogtown_ProfessorLab_EventScript_DontLeave {
    lockall
    msgbox(format("You can't leave yet, {PLAYER}! You need to choose a starter Pokémon first!"))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, Playerup)
    waitmovement(0)
    releaseall
}

script PacifidlogTown_ProfessorLab_EventScript_TriggerRival_4 {
    call(PacifidlogTown_ProfessorLab_EventScript_RivalIntro)
    applymovement(OBJ_EVENT_ID_PLAYER, faceright)
    msgbox(format("Oh, {PLAYER}, looks like you beat me here!"))
    waitmessage
    applymovement(7, rivalmove)
    applymovement(OBJ_EVENT_ID_PLAYER, uptwo)
    waitmovement(7)
    call(PacifidlogTown_ProfessorLab_EventScript_RivalStarterSelection)
    releaseall
}

script PacifidlogTown_ProfessorLab_EventScript_TriggerRival_5 {
    call(PacifidlogTown_ProfessorLab_EventScript_RivalIntro)
    applymovement(OBJ_EVENT_ID_PLAYER, facedown)
    msgbox(format("Oh, {PLAYER}, looks like you beat me here!"))
    waitmessage
    applymovement(7, rivalmove)
    applymovement(OBJ_EVENT_ID_PLAYER, leftup)
    waitmovement(7)
    call(PacifidlogTown_ProfessorLab_EventScript_RivalStarterSelection)
    releaseall
}

script PacifidlogTown_ProfessorLab_EventScript_TriggerRival_6 {
    call(PacifidlogTown_ProfessorLab_EventScript_RivalIntro)
    applymovement(OBJ_EVENT_ID_PLAYER, facedown)
    msgbox(format("Oh, {PLAYER}, looks like you beat me here!"))
    waitmessage
    applymovement(7, rivalmove)
    applymovement(OBJ_EVENT_ID_PLAYER, waitleft)
    waitmovement(7)
    call(PacifidlogTown_ProfessorLab_EventScript_RivalStarterSelection)
    releaseall
}

script PacifidlogTown_ProfessorLab_EventScript_TriggerRival_7 {
    call(PacifidlogTown_ProfessorLab_EventScript_RivalIntro)
    applymovement(OBJ_EVENT_ID_PLAYER, faceleft)
    msgbox(format("Oh, {PLAYER}, looks like you beat me here!"))
    waitmessage
    applymovement(7, rivalmove)
    applymovement(OBJ_EVENT_ID_PLAYER, left3up)
    waitmovement(7)
    call(PacifidlogTown_ProfessorLab_EventScript_RivalStarterSelection)
    releaseall
}
script PacifidlogTown_ProfessorLab_EventScript_RivalIntro {
    lockall
    addobject(7)    
    msgbox(format("Ah, sorry I'm late Professor!"))
    waitmessage
    applymovement(7, facetheplayer)
    
}

script PacifidlogTown_ProfessorLab_EventScript_RivalStarterSelection {
    lockall
    msgbox(format("Oh, RIVAL, you can choose a starter Pokémon too!"))
    waitmessage
    msgbox(format("Hmm... I think I'll take..."))
    if(var(VAR_STARTER_MON) == 0) {
        applymovement(7, tomiddlemon)
        waitmovement(0)
        msgbox(format("Oshawott!"))
        removeobject(5)
        setflag(FLAG_MID_STARTER)
        waitmessage
        applymovement(7, right1)
        waitmovement(0)
        applymovement(7, facetheplayer)
        waitmovement(0)
        msgbox(format("Well {PLAYER}, we've both got a Pokémon now! You know what that means, right?"))
    }
    elif(var(VAR_STARTER_MON) == 1) {
        applymovement(7, torightmon)
        waitmovement(0)
        msgbox(format("Popplio!"))
        removeobject(6)
        setflag(FLAG_RIGHT_STARTER)
        waitmessage
        applymovement(7, facetheplayer)
        waitmovement(0)
        msgbox(format("Well {PLAYER}, we've both got a Pokémon now! You know what that means, right?"))
    }
    else {
        applymovement(7, toleftmon)
        waitmovement(0)
        msgbox(format("Piplup!"))
        removeobject(4)
        setflag(FLAG_LEFT_STARTER)
        waitmessage
        applymovement(7, right2)
        waitmovement(0)
        applymovement(7, facetheplayer)
        waitmovement(0)
        msgbox(format("Well {PLAYER}, we've both got a Pokémon now! You know what that means, right?"))
    }
    waitmessage
    msgbox(format("This is where I'd put my rival battle, if I had one."))
    setvar(VAR_PACIFIDLOG_TOWN_STATE, 4)
    releaseall

    
}
movement ProfessorIntro1 {
    walk_down 
    walk_right * 2
    face_up
}

movement ProfessorIntro2 {
    walk_left * 2
    emote_exclamation_mark
}
    
movement ProfessorIntro3 {
    walk_up
    walk_left
    walk_up
    walk_left
    walk_up
    face_down
}    
    
movement PlayerIntro {
    walk_up * 2
    walk_left * 2
    walk_up
}

movement Playerup {
    walk_up
}

movement facetheplayer {
    face_player
}

movement faceright {
    face_right
}

movement facedown {
    face_down
}

movement faceleft {
    face_left
}

movement rivalmove {
    walk_up * 3
    walk_left * 2 
    face_up
}

movement uptwo {
    walk_up * 2
}

movement leftup {
    walk_left
    walk_up
}

movement waitleft {
    delay_1
    walk_left * 2
    walk_up
}
movement left3up {
    walk_left * 3
    walk_up *2
}

movement toleftmon {
    walk_left
    walk_up
}

movement tomiddlemon {
    walk_up
}

movement torightmon {
    walk_right
    walk_up
}

movement right2 {
    walk_right * 2
}

movement right1 {
    walk_right
}